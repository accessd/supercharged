class SuperchargedForm
  constructor: (selector, options = {}) ->
    @form = $(selector)
    return if @form.length == 0

    @amount_input = @form.find("[role='charge-amount']")
    @id_input = @form.find("[value='<%= Supercharged::ChargesHelper::FAKE_ORDER_ID %>']")

    @form.submit =>
      @startPayment()

      unless parseInt(@id_input.val())
        alert("Error: undefined charge id")
        return false

  startPayment: ->
    @createInternalTransaction(
      success: (charge) =>
        @prepareGatewayForm(charge)
    )

  createInternalTransaction: (options) ->
    charge_attributes = @getChargeAttributes()
    $.ajax(
      url: "/charges.json",
      type: "POST",
      async: false,
      data: {charge: charge_attributes},
      success: (response) ->
        options.success(response)
    )

  getChargeAttributes: ->
    {
      amount: @amount_input.val()
    }

  prepareGatewayForm: (charge) ->
    @id_input.val(charge.id)

$ ->
  new SuperchargedForm("[role='gateway-charge-form']")
